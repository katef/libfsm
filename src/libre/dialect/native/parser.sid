/*
 * Copyright 2008-2017 Katherine Flavel
 *
 * See LICENCE for the full copyright terms.
 */

/*
 * TODO: explain choices. e.g. i'm not permitting () or ||. only the root expression is allowed to be empty.
 * TODO: explain strategy: pass in x,y endpoints top-down
 */

%types%

	pos;
	char;
	unsigned;
	!re_flags;
	!err;
	ast_expr;
	ast_class_id;
	ast_count;
	endpoint;

%terminals%

	ANY;
	!MANY;
	OPT;
	PLUS;
	ALT;
	SEP;
	STAR;

	START;
	END;

	OPENSUB;   CLOSESUB;
	OPENGROUP: () -> (:pos, :pos); CLOSEGROUP: () -> (:char, :pos, :pos);
	OPENCOUNT: () -> (:pos, :pos); CLOSECOUNT: () -> (:pos, :pos);

	!OPENGROUPINV: () -> (:pos, :pos);
	!OPENGROUPCB: () -> (:pos, :pos);
	!OPENGROUPINVCB: () -> (:pos, :pos);

	INVERT: () -> (:char);
	RANGE:  () -> (:char, :pos, :pos);

	NAMED_CLASS: () -> (:ast_class_id, :pos, :pos);

	!OPENFLAGS;
	!CLOSEFLAGS;
	!NEGATE;
	!FLAG_UNKNOWN;
	!FLAG_INSENSITIVE: () -> (:re_flags);

	ESC:      () -> (:char, :pos, :pos);
	!NOESC:   () -> (:char, :pos, :pos);
	!CONTROL: () -> (:char, :pos, :pos);
	OCT:      () -> (:char, :pos, :pos);
	HEX:      () -> (:char, :pos, :pos);
	CHAR:     () -> (:char, :pos, :pos);
	COUNT:    () -> (:unsigned);

	EOF;
	!ERROR;

%productions%

	!<re-flag-none>:  ()                     -> (:re_flags);
	!<re-flag-union>: (:re_flags, :re_flags) -> (:re_flags);

	!<count-unbounded>:	  () -> (:unsigned);
	<count-zero-or-more>: () -> (:ast_count);
	<count-one-or-more>:  () -> (:ast_count);
	<count-zero-or-one>:  () -> (:ast_count);
	<count-one>:          () -> (:ast_count);
	<count-range>: (:unsigned, :pos, :unsigned, :pos) -> (:ast_count);

	!<make-literal-cbrak>:    ()                       -> (:char);

	<ast-range-endpoint-literal>: (:char)         -> (:endpoint);
	!<ast-range-endpoint-class>:  (:ast_class_id) -> (:endpoint);
	<ast-range-distinct>: (:endpoint, :pos, :endpoint, :pos) -> ();

	<ast-make-empty>:        ()                       -> (:ast_expr);
	<ast-make-literal>:      (:char)                  -> (:ast_expr);
	<ast-make-concat>:       ()                       -> (:ast_expr);
	<ast-make-alt>:          ()                       -> (:ast_expr);
	<ast-make-any>:          ()                       -> (:ast_expr);
	<ast-make-piece>:        (:ast_expr, :ast_count)  -> (:ast_expr);
	<ast-make-group>:        (:ast_expr)              -> (:ast_expr);
	!<ast-make-re-flags>:    (:re_flags, :re_flags)   -> (:ast_expr);
	<ast-make-anchor-start>: ()                       -> (:ast_expr);
	<ast-make-anchor-end>:   ()                       -> (:ast_expr);
	!<ast-make-subtract>:    (:ast_expr, :ast_expr)   -> (:ast_expr); 
	<ast-make-invert>:       (:ast_expr)              -> (:ast_expr); 
	<ast-make-range>:        (:endpoint, :pos, :endpoint, :pos) -> (:ast_expr);
	<ast-make-named>:        (:ast_class_id)          -> (:ast_expr);

	<ast-add-concat>: (:ast_expr, :ast_expr) -> ();
	<ast-add-alt>:    (:ast_expr, :ast_expr) -> ();

	<err-expected-term>;
	<err-expected-count>;
	<err-expected-atom>;
	<err-expected-alts>;
	<err-expected-range>;
	<err-expected-closegroup>;
	!<err-unknown-flag>;
	!<err-expected-closeflags>;
	!<err-expected-groupbody>;
	!<err-unknown-flag>;
	!<err-expected-closeflags>;
	<err-expected-eof>;
	!<err-unsupported>;

	<mark-group>: (:pos, :pos) -> ();
	<mark-range>: (:pos, :pos) -> ();
	<mark-count>: (:pos, :pos) -> ();
	<mark-expr>: (:ast_expr, :pos, :pos) -> ();

	expr: () -> (node :ast_expr) [

		literal: () -> (node :ast_expr) = {
			{
				(c, !, !) = ESC;
			||
				(c, !, !) = OCT;
			||
				(c, !, !) = HEX;
			||
				(c, !, !) = CHAR;
			};
			node = <ast-make-literal>(c);
  		};

		character-class: () -> (node :ast_expr) [

			/* TODO: []] [-]] etc.? though they can be escaped. */
			class-head: (class :ast_expr &) -> () = {
				! = INVERT;
				&class = <ast-make-invert>(class);
			||
				$;
			};

			class-literal: () -> (node :ast_expr) = {
				{
					(c, !, !) = ESC;
				||
					(c, !, !) = OCT;
				||
					(c, !, !) = HEX;
				||
					(c, !, !) = CHAR;
				};
				node = <ast-make-literal>(c);
	  		};

			class-range: () -> (node :ast_expr) = {
				{
					(ca, start, !) = ESC;
				||
					(ca, start, !) = OCT;
				||
					(ca, start, !) = HEX;
				||
					(ca, start, !) = CHAR;
				};
				a = <ast-range-endpoint-literal>(ca);

				{
					(!, !, !) = RANGE;
				##
					<err-expected-range>;
				};

				{
					(cz, !, end) = ESC;
				||
					(cz, !, end) = OCT;
				||
					(cz, !, end) = HEX;
				||
					(cz, !, end) = CHAR;
				||
					(cz, !, end) = RANGE;
				};
				z = <ast-range-endpoint-literal>(cz);

				<mark-range>(start, end);
				<ast-range-distinct>(a, start, z, end);

				node = <ast-make-range>(a, start, z, end);
			};

			class-named: () -> (node :ast_expr) = {
				(id, !, !) = NAMED_CLASS;
				node = <ast-make-named>(id);
			};

			class-term: () -> (node :ast_expr) = {
				node = class-literal;
			||
				node = class-range;
			||
				node = class-named;
			};

			list-of-class-terms: (class :ast_expr) -> () = {
				{
					node = class-term;
					<ast-add-alt>(class, node);
				##
					<err-expected-term>;
				};

				{
					list-of-class-terms(class);
				||
					$;
				};
			};

		] = {
			(start, !) = OPENGROUP;

			node = <ast-make-alt>;
			tmp = node;

			class-head(&node);
			list-of-class-terms(tmp);

			{
				(!, !, end) = CLOSEGROUP;
				<mark-group>(start, end);
			##
				<err-expected-closegroup>;
				end = start; /* appease sid */
			};

			<mark-expr>(tmp, start, end);
		};

		piece: () -> (node :ast_expr) [

			atom: () -> (e :ast_expr) = {
				START;
				e = <ast-make-anchor-start>;
			||
				END;
				e = <ast-make-anchor-end>;
			||
				ANY;
				e = <ast-make-any>;
			||
				e = literal;
			||
				e = character-class;
			||
				OPENSUB;
				g = expr;
				e = <ast-make-group>(g);
				CLOSESUB;
			##
				<err-expected-atom>;
				e = <ast-make-any>;
			};

			count: () -> (c :ast_count) = {
				STAR;
				c = <count-zero-or-more>;
			||
				PLUS;
				c = <count-one-or-more>;
			||
				OPT;
				c = <count-zero-or-one>;
			||
				(start, !) = OPENCOUNT;
				m = COUNT;
				(!, end) = CLOSECOUNT;
				<mark-count>(start, end);
				c = <count-range>(m, start, m, end);
			||
				(start, !) = OPENCOUNT;
				m = COUNT;
				SEP;
				n = COUNT;
				(!, end) = CLOSECOUNT;
				<mark-count>(start, end);
				c = <count-range>(m, start, n, end);
			||
				c = <count-one>;
			##
				<err-expected-count>;
				c = <count-one>;
			};

		] = {
			e = atom;
			c = count;

			node = <ast-make-piece>(e, c);
		};

		list-of-pieces: (cat :ast_expr) -> () = {
			a = piece;
			<ast-add-concat>(cat, a);

			{
				list-of-pieces(cat);
			||
				$;
			};
		};

		alt: () -> (node :ast_expr) = {
			node = <ast-make-concat>;
			list-of-pieces(node);

			/* we don't allow empty alts for this dialect */
		};

		list-of-alts: (alts :ast_expr) -> () = {
			a = alt;

			<ast-add-alt>(alts, a);

			{
				ALT;

				list-of-alts(alts);
			||
				$;
			};
		##
			<err-expected-alts>;
		};

	] = {
		node = <ast-make-alt>;
		list-of-alts(node);
	||
		/* alts cannot be empty, but an entire expr can be empty */
		node = <ast-make-empty>;
	##
		<err-expected-alts>;
		node = <ast-make-empty>;
	};

	re_native: () -> (node :ast_expr) = {
		{
			node = expr;
		};

		{
			EOF;
		##
			<err-expected-eof>;
		};
	};

%entry%

	re_native;


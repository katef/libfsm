/*
 * Copyright 2008-2017 Katherine Flavel
 *
 * See LICENCE for the full copyright terms.
 */

/*
 * TODO: explain choices. e.g. i'm not permitting () or ||. only the root expression is allowed to be empty.
 * TODO: explain strategy: pass in x,y endpoints top-down
 */

%types%

	pos;
	char;
	unsigned;
	!re_flags;
	!err;
	ast_expr;
	ast_class_id;
	ast_count;
	endpoint;
	group_id;

%terminals%

	ANY;
	!MANY;
	!EOL;
	OPT;
	PLUS;
	ALT;
	SEP;
	STAR;

	START;
	END;
	!END_NL;

	OPENSUB;   CLOSESUB;
	OPENGROUP: () -> (:pos, :pos); CLOSEGROUP: () -> (:char, :pos, :pos);
	OPENGROUPINV: () -> (:pos, :pos);
	OPENGROUPCB: () -> (:pos, :pos);
	OPENGROUPINVCB: () -> (:pos, :pos);
	CLOSEGROUPRANGE: () -> (:char, :pos, :pos);
	OPENCOUNT: () -> (:pos, :pos); CLOSECOUNT: () -> (:pos, :pos);

	!INVERT: () -> (:char);
	RANGE:  () -> (:char, :pos, :pos);

	NAMED_CLASS: () -> (:ast_class_id, :pos, :pos);

	!OPENFLAGS;
	!CLOSEFLAGS;
	!NEGATE;
	!FLAG_IGNORE;
	!FLAG_UNKNOWN;
	!FLAG_EXTENDED: () -> (:re_flags);
	!FLAG_SINGLE: () -> (:re_flags);
	!FLAG_INSENSITIVE: () -> (:re_flags);

	!WHITESPACE: () -> (:char, :pos, :pos);
	!NEWLINE: () -> (:char, :pos, :pos);
	!MAYBE_COMMENT: () -> (:char, :pos, :pos);

	ESC:      () -> (:char, :pos, :pos);
	!NOESC:   () -> (:char, :pos, :pos);
	!CONTROL: () -> (:char, :pos, :pos);
	OCT:      () -> (:char, :pos, :pos);
	HEX:      () -> (:char, :pos, :pos);
	CHAR:     () -> (:char, :pos, :pos);
	COUNT:    () -> (:unsigned);

	!UNSUPPORTED:     () -> (:char, :pos, :pos);

	EOF;
	!ERROR;

%productions%

	!<re-flag-none>:  ()                     -> (:re_flags);
	!<re-flag-union>: (:re_flags, :re_flags) -> (:re_flags);

	!<count-unbounded>:	  () -> (:unsigned);
	<count-zero-or-more>: () -> (:ast_count);
	<count-one-or-more>:  () -> (:ast_count);
	<count-zero-or-one>:  () -> (:ast_count);
	<count-one>:          () -> (:ast_count);
	<count-range>: (:unsigned, :pos, :unsigned, :pos) -> (:ast_count);

	<make-group-id>:       () -> (:group_id);
	<make-literal-cbrak>:  () -> (:char);
	!<make-literal-cr>:    () -> (:char);
	!<make-literal-nl>:    () -> (:char);

	<ast-range-endpoint-literal>: (:char)         -> (:endpoint);
	<ast-range-endpoint-class>:   (:ast_class_id) -> (:endpoint);

	<ast-make-empty>:        ()                       -> (:ast_expr);
	<ast-make-literal>:      (:char)                  -> (:ast_expr);
	<ast-make-concat>:       ()                       -> (:ast_expr);
	<ast-make-alt>:          ()                       -> (:ast_expr);
	<ast-make-piece>:        (:ast_expr, :ast_count)  -> (:ast_expr);
	<ast-make-group>:        (:ast_expr, :group_id)   -> (:ast_expr);
	!<ast-get-re-flags>:     ()                       -> (:re_flags);
	!<ast-set-re-flags>:     (:re_flags)              -> ();
	!<ast-mask-re-flags>:    (:re_flags, :re_flags)   -> ();
	<ast-make-anchor-start>: ()                       -> (:ast_expr);
	<ast-make-anchor-end>:   ()                       -> (:ast_expr);
	!<ast-make-anchor-end-nl>:()                      -> (:ast_expr);
	!<ast-make-subtract>:    (:ast_expr, :ast_expr)   -> (:ast_expr); 
	<ast-make-invert>:       (:ast_expr)              -> (:ast_expr); 
	<ast-make-range>:        (:endpoint, :pos, :endpoint, :pos) -> (:ast_expr);
	<ast-make-named>:        (:ast_class_id)          -> (:ast_expr);

	<ast-add-concat>: (:ast_expr, :ast_expr) -> ();
	<ast-add-alt>:    (:ast_expr, :ast_expr) -> ();

	!<err-expected-term>;
	<err-expected-count>;
	<err-expected-atom>;
	<err-expected-alts>;
	<err-expected-range>;
	<err-expected-closegroup>;
	!<err-unknown-flag>;
	!<err-expected-closeflags>;
	!<err-expected-groupbody>;
	!<err-unknown-flag>;
	!<err-expected-closeflags>;
	<err-expected-eof>;
	!<err-unsupported>;
	!<err-invalid-comment>;

	<mark-group>: (:pos, :pos) -> ();
	<mark-range>: (:pos, :pos) -> ();
	<mark-count>: (:pos, :pos) -> ();
	<mark-expr>: (:ast_expr, :pos, :pos) -> ();

	<class-any>:  () -> (:ast_class_id);
	!<class-bsr>: () -> (:ast_class_id);

	expr: () -> (node :ast_expr) [

		literal: () -> (node :ast_expr) = {
			{
				(c, !, !) = ESC;
			||
				(c, !, !) = OCT;
			||
				(c, !, !) = HEX;
			||
				(c, !, !) = CHAR;
			};
			node = <ast-make-literal>(c);
  		};

		character-class: () -> (node :ast_expr) [

			class-literal: () -> (node :ast_expr) = {
				{
					(c, !, !) = ESC;
				||
					(c, !, !) = OCT;
				||
					(c, !, !) = HEX;
				||
					(c, !, !) = CHAR;
				};

				node = <ast-make-literal>(c);
	  		};

			::class-named: () -> (node :ast_expr, start :pos, end :pos) = {
				(id, start, end) = NAMED_CLASS;
				node = <ast-make-named>(id);
			};

			range-endpoint: () -> (r :endpoint, start :pos, end :pos) [
				range-endpoint-literal: () -> (r :endpoint, start :pos, end :pos) = {
					{
						(c, start, end) = ESC;
					||
						(c, start, end) = OCT;
					||
						(c, start, end) = HEX;
					||
						(c, start, end) = CHAR;
					};
					r = <ast-range-endpoint-literal>(c);
				};

				range-endpoint-class: () -> (r :endpoint, start :pos, end :pos) = {
					(id, start, end) = NAMED_CLASS;
					r = <ast-range-endpoint-class>(id);
				};
			] = {
				{
					(r, start, end) = range-endpoint-literal;
				||
					(r, start, end) = range-endpoint-class;
				};
			};

			range-endpoint-end: () -> (r :endpoint, end :pos) = {
				{
					(r, !, end) = range-endpoint;
				||
					/* allow literal '-' as end of range */
					(c, !, end) = RANGE;
					r = <ast-range-endpoint-literal>(c);
				};
			};

			class-range: () -> (node :ast_expr) = {
				(lower, start, !) = range-endpoint;

				{
					(!, !, !) = RANGE;
				##
					<err-expected-range>;
				};

				(upper, end) = range-endpoint-end;

				<mark-range>(start, end);
				node = <ast-make-range>(lower, start, upper, end);
			};

			class-term: () -> (node :ast_expr) = {
				node = class-literal;
			||
				node = class-range;
			||
				(node, !, !) = class-named;
			};

			list-of-class-terms: (class :ast_expr) -> () = {
				node = class-term;
				<ast-add-alt>(class, node);
				list-of-class-terms(class);
			||
				$;
			};

		] = {
			{
				(start, !) = OPENGROUP;

				node = <ast-make-alt>;
				tmp = node;

				{
					(c, rstart, !) = RANGE;

					{
						lower = <ast-range-endpoint-literal>(c);
						(!, !, !) = RANGE;
						(upper,end) = range-endpoint-end;
						node1 = <ast-make-range>(lower, rstart, upper, end);
					||
						node1 = <ast-make-literal>(c);
					};

					<ast-add-alt>(tmp,node1);
				||
					$;
				};

				list-of-class-terms(tmp);
			||
				(start, !) = OPENGROUPINV;

				node = <ast-make-alt>;
				tmp  = node;

				&node = <ast-make-invert>(node);

				{
					(c, rstart, !) = RANGE;

					{
						lower = <ast-range-endpoint-literal>(c);
						(!, !, !) = RANGE;
						(upper, end) = range-endpoint-end;
						node1 = <ast-make-range>(lower, rstart, upper, end);
					||
						node1 = <ast-make-literal>(c);
					};

					<ast-add-alt>(tmp,node1);
				||
					$;
				};

				list-of-class-terms(tmp);

			||
				(start, !) = OPENGROUPCB;

				node = <ast-make-alt>;
				tmp  = node;

				cbrak = <make-literal-cbrak>;
				{
					r = <ast-range-endpoint-literal>(cbrak);
					(!, !, !) = RANGE;
					(upper, end) = range-endpoint-end;
					/* FIXME: start is off by one */
					lower = <ast-range-endpoint-literal>(cbrak);
					node1 = <ast-make-range>(lower, start, upper, end);
				||
					node1 = <ast-make-literal>(cbrak);
				};

				<ast-add-alt>(tmp, node1);

				list-of-class-terms(tmp);
			||
				(start, !) = OPENGROUPINVCB;

				node = <ast-make-alt>;
				tmp  = node;

				&node = <ast-make-invert>(node);

				cbrak = <make-literal-cbrak>;
				{
					r = <ast-range-endpoint-literal>(cbrak);
					(!, !, !) = RANGE;
					(upper, end) = range-endpoint-end;
					/* FIXME: start is off by one */
					lower = <ast-range-endpoint-literal>(cbrak);
					node1 = <ast-make-range>(lower, start, upper, end);
				||
					node1 = <ast-make-literal>(cbrak);
				};

				<ast-add-alt>(tmp, node1);

				list-of-class-terms(tmp);
			};

			{
				(!, !, end) = CLOSEGROUP;
				<mark-group>(start, end);
			||
				(crange, !, end) = CLOSEGROUPRANGE;
				range = <ast-make-literal>(crange);
				<ast-add-alt>(tmp, range);
				<mark-group>(start, end);
			##
				<err-expected-closegroup>;
				end = start; /* appease sid */
			};

			<mark-expr>(tmp, start, end);
		};

		piece: () -> (node :ast_expr) [

			atom: () -> (e :ast_expr) = {
				START;
				e = <ast-make-anchor-start>;
			||
				END;
				e = <ast-make-anchor-end>;
			||
				ANY;
				a = <class-any>;
				e = <ast-make-named>(a);
			||
				e = literal;
			||
				e = character-class;
			||
				OPENSUB;
				id = <make-group-id>;
				g = expr;
				e = <ast-make-group>(g, id);
				CLOSESUB;
			##
				<err-expected-atom>;
				e = <ast-make-empty>;
			};

			count: () -> (c :ast_count) = {
				STAR;
				c = <count-zero-or-more>;
			||
				PLUS;
				c = <count-one-or-more>;
			||
				OPT;
				c = <count-zero-or-one>;
			||
				(start, !) = OPENCOUNT;
				m = COUNT;
				(!, end) = CLOSECOUNT;
				<mark-count>(start, end);
				c = <count-range>(m, start, m, end);
			||
				(start, !) = OPENCOUNT;
				m = COUNT;
				SEP;
				n = COUNT;
				(!, end) = CLOSECOUNT;
				<mark-count>(start, end);
				c = <count-range>(m, start, n, end);
			||
				c = <count-one>;
			##
				<err-expected-count>;
				c = <count-one>;
			};

		] = {
			e = atom;
			c = count;

			node = <ast-make-piece>(e, c);
		};

		list-of-pieces: (cat :ast_expr) -> () = {
			a = piece;
			<ast-add-concat>(cat, a);

			{
				list-of-pieces(cat);
			||
				$;
			};
		};

		alt: () -> (node :ast_expr) = {
			node = <ast-make-concat>;
			list-of-pieces(node);

			/* we don't allow empty alts for this dialect */
		};

		list-of-alts: (alts :ast_expr) -> () = {
			a = alt;

			<ast-add-alt>(alts, a);

			{
				ALT;

				list-of-alts(alts);
			||
				$;
			};
		##
			<err-expected-alts>;
		};

	] = {
		node = <ast-make-alt>;
		list-of-alts(node);
	||
		/* alts cannot be empty, but an entire expr can be empty */
		node = <ast-make-empty>;
	##
		<err-expected-alts>;
		node = <ast-make-empty>;
	};

	re_native: () -> (node :ast_expr) = {
		id = <make-group-id>;
		e = expr;
		node = <ast-make-group>(e, id);

		{
			EOF;
		##
			<err-expected-eof>;
		};
	};

%entry%

	re_native;


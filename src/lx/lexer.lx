#
# Copyright 2008-2017 Katherine Flavel
#
# See LICENCE for the full copyright terms.
#

/[\r\n\t ]+/;
"#" .. "\n";

"'" .. "'" -> $str {
	# XXX: would be /.+/ here but this incorrectly includes /exit/
	/./ -> $char;
}

'"' .. '"' -> $str {
	'\\' -> $esc;
	'\"' -> $esc;
	'\f' -> $esc;
	'\n' -> $esc;
	'\r' -> $esc;
	'\t' -> $esc;
	'\v' -> $esc;

	'\'  /[0-7]+/     -> $oct;
	'\x' /[0-9a-f]+/i -> $hex;

	/./ - ($esc | $oct | $hex) -> $char;
}

'/' .. '/' /[a-z]*/i -> $re {
	/./ -> $char;
}

'='  -> $bind;
';'  -> $semi;
'..' -> $to;
'->' -> $map;

'{' -> $open;   '}' -> $close;
'(' -> $lparen; ')' -> $rparen;

# regexp operators
'*' -> $star;
'+' -> $cross;
'?' -> $qmark;

# prefix operators
'~' -> $tilde;
'!' -> $bang;
'^' -> $hat; # complete

# binary operators
'-' -> $dash;
'\' -> $dash; # synonym for '-'
'.' -> $dot;
'|' -> $pipe;
'&' -> $and; # intersection

/[a-z_][a-z0-9_]*/i -> $ident;
'$' $ident -> $token;

